import org.gradle.internal.os.OperatingSystem

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.1"
    }
}

switch ( OperatingSystem.current() ) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
}

subprojects {
    apply plugin: 'io.spring.dependency-management'

    repositories {
        jcenter()
    }

    dependencyManagement {
        dependencies {
            dependencySet(group:'org.jetbrains.kotlin', version: kotlinVersion) {
                entry 'kotlin-stdlib-common'
                entry 'kotlin-stdlib-jdk8'
                entry 'kotlin-reflect'
            }

            dependencySet(group:'org.jetbrains.kotlinx', version: '0.19.3') {
                entry('kotlinx-coroutines-core') {
                    exclude 'org.jetbrains.kotlin:kotlin-stdlib'
                }
            }

            dependencySet(group: 'org.lwjgl', version: lwjglVersion) {
                entry "lwjgl"
                entry "lwjgl-glfw"
                entry "lwjgl-opengl"
            }

            dependency 'org.joml:joml:1.9.6'

            dependency 'org.roaringbitmap:RoaringBitmap:0.6.51'

            dependency 'com.nhaarman:mockito-kotlin:1.5.0'

            dependency 'com.natpryce:hamkrest:1.4.2.2'

            dependency 'org.mockito:mockito-core:2.11.0'

            dependencySet(group: 'org.junit.platform', version: '1.0.1') {
                entry 'junit-platform-engine'
                entry 'junit-platform-runner'
                entry 'junit-platform-launcher'
            }

            dependencySet(group: 'org.jetbrains.spek', version: '1.1.5') {
                entry 'spek-api'
                entry 'spek-subject-extension'
                entry 'spek-junit-platform-engine'
            }

        }
    }
}
